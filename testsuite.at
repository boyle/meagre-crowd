AT_INIT()
AT_COPYRIGHT([Copyright (C) 2010, Alistair Boyle])
AT_COLOR_TESTS




AT_BANNER([hello-world])
AT_SETUP([mpi default: 1 node])
AT_KEYWORDS([mpi hello])
AT_CHECK([helloworld-mpi],0,[Hello world from process 0 of 1
])
AT_CLEANUP

AT_SETUP([mpi: 1 node])
AT_KEYWORDS([mpi hello])
AT_CHECK([mpirun -n 1 helloworld-mpi],0,[Hello world from process 0 of 1
])
AT_CLEANUP

AT_SETUP([mpi: 2 nodes])
AT_KEYWORDS([mpi hello])
AT_CHECK([mpirun -n 2 helloworld-mpi],0,ignore)
AT_CLEANUP


AT_SETUP([mpi: 3 nodes])
AT_KEYWORDS([mpi hello])
AT_CHECK([mpirun -n 3 helloworld-mpi],0,ignore)
AT_CLEANUP

AT_SETUP([mpi: 4 nodes])
AT_KEYWORDS([mpi hello])
AT_CHECK([mpirun -n 4 helloworld-mpi],0,ignore)
AT_CLEANUP


AT_SETUP([mumps])
AT_KEYWORDS([mumps hello])
AT_CHECK([mpirun -n 4 helloworld-mumps],0,[Solution is ( 1.00; 2.00 ): PASS
])
AT_CLEANUP

AT_SETUP([matrix-market files])
AT_KEYWORDS([file hello])
AT_DATA([test.rb],[Small general matrix used as Example 1                                  EXAMPLE1
             5             1             1             3             0
RUA                        5             5            11
(3I6)           (11I3)          (5E15.8)           
  1  4  6  8 10 12
  1  3  5  1  4  2  5  1  4  2  5
 1.0            2.0            3.0           -4.0            5.0
-6.0           -7.0           -8.0           -9.0            10.0
11.0
])
AT_DATA([test.hb],[Title                                                                   Key     
             5             1             1             3             0
RUA                        5             5            13             0
(6I3)           (13I3)          (5E15.8)            (5E15.8)            
  1  4  7  8 11 14
  1  3  5  2  3  5  3  1  3  4  3  4  5
11.0           31.0           51.0           22.0           32.0
52.0           33.0           14.0           34.0           44.0
35.0           45.0           55.0
])
AT_DATA([test.mm],[%%MatrixMarket matrix coordinate real general
%%RBCode matrix
%%RBMatrixID EXAMPLE1
%%RBTitle Small general matrix used as Example 1
5  5  11
1  1   1.0
3  1   2.0
5  1   3.0
1  2  -4.0
4  2   5.0
2  3  -6.0
5  3  -7.0
1  4  -8.0
4  4  -9.0
2  5  10.0
5  5  11.0
])
AT_CHECK([file],0,[harwell-boeing
rutherford-boeing
matrix market
PASS
])
AT_CLEANUP

AT_SETUP([file-based mumps])
AT_KEYWORDS([mumps-file hello])
AT_DATA([test.mm],[%%MatrixMarket matrix coordinate real general
%%RBCode matrix
%%RBMatrixID EXAMPLE1
%%RBTitle Small general matrix used as Example 1
5  5  11
1  1   1.0
3  1   2.0
5  1   3.0
1  2  -4.0
4  2   5.0
2  3  -6.0
5  3  -7.0
1  4  -8.0
4  4  -9.0
2  5  10.0
5  5  11.0
])
AT_CHECK(mpirun -n 4 mumps-file,0,ignore)
AT_CLEANUP

AT_BANNER([help])

m4_define([MC_CHECK_HELP_NOCRASH],[
AT_SETUP([$1])
AT_KEYWORDS([help])
AT_CHECK(AT_PACKAGE_NAME[ $1],,ignore)
AT_CLEANUP
])

m4_define([MC_CHECK_HELP_VER],[
AT_SETUP([$1])
AT_KEYWORDS([help])
AT_CHECK(AT_PACKAGE_NAME[ $1],,AT_PACKAGE_NAME[ ]AT_PACKAGE_VERSION[
])
AT_CLEANUP
])


MC_CHECK_HELP_NOCRASH([--help])
MC_CHECK_HELP_NOCRASH([-h])
MC_CHECK_HELP_NOCRASH([-?])
MC_CHECK_HELP_VER([-V])
MC_CHECK_HELP_VER([--version])





AT_BANNER([unit tests])
AT_SETUP([perftimer])
AT_KEYWORDS([unit])
AT_CHECK([unit_perftimer],,ignore)
AT_CLEANUP()
